// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
    groupId = 'com.solitics'
}
buildscript {

    repositories {
        mavenLocal()
        google()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.0'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.29.0'
    }
}
ext {
    artifactory_url      = 'https://soliticsmobilesdk.jfrog.io/artifactory'
    artifactory_repoKey  = 'android.solitics.mobile.releases'
    artifactory_username = System.getenv('solitics.consumer.username') ?: findProperty('solitics.consumer.username')
    artifactory_password = System.getenv('solitics.consumer.password') ?: findProperty('solitics.consumer.password')
}

allprojects { project ->
    apply plugin: 'com.jfrog.artifactory'

    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }

    project.afterEvaluate { module ->

        module.configurations.all {

            resolutionStrategy {
                // cache dynamic versions for 10 minutes
                // cacheDynamicVersionsFor 10*60, 'seconds'
                cacheDynamicVersionsFor 0, 'seconds'

                // don't cache changing modules at all
                cacheChangingModulesFor 0, 'seconds'
            }
        }

        module.tasks.withType(JavaCompile) { task ->
            task.options.encoding = 'UTF-8'
            task.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Xlint:-options"
        }
        module.tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            options.addStringOption('encoding', 'UTF-8')
            options.addStringOption('charSet', 'UTF-8')
        }
    }
}
artifactory {
    contextUrl = artifactory_url // The base Artifactory URL if not overridden by the publisher/resolver
    publish {}
    resolve {
        repository {
            repoKey  = artifactory_repoKey
            username = artifactory_username
            password = artifactory_password
            maven = true
        }
    }
}